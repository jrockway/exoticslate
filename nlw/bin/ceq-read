#!/usr/bin/env perl
# @COPYRIGHT@
use warnings;
use strict;

use Getopt::Long;
use Pod::Usage 1.12;
use FindBin;
use lib "$FindBin::Bin/../lib";

GetOptions(
    'h|help|?'      => sub {pod2usage({-verbose => 1, -input => $0}); exit},
    'H|man'         => sub {pod2usage({-verbose => 2, -input => $0}); exit},
) or die;

use Socialtext::Ceqlotron 'foreach_event';

use DateTime;

foreach_event( sub {
    my ( $event ) = @_;

    my $attachment_args = sub {
        ( 'A', $event->workspace_name, $event->page_uri, $event->attachment_id);
    };
    my $page_args = sub { ( 'P', $event->workspace_name, $event->page_uri ) };
    my $workspace_args = sub { ( 'W', $event->workspace_name ) };

    print_event(
        $event->link_path,
          $event->isa('Socialtext::ChangeEvent::Attachment') ? &$attachment_args
        : $event->isa('Socialtext::ChangeEvent::Page')       ? &$page_args
        : &$workspace_args
    );
}, sub {
    my ( $path, $exception ) = @_;

    print_event("?", $path, "UNKNOWN") if $exception =~ /cequndeflink/;
} );

sub print_event {
    my ( $path, $marker, @name_elements ) = @_;
    my $ctime = (lstat($path))[10];

    # This happens if the symlink got removed.
    return if $!{ENOENT};

    die "lstat '$path' failed: $!" unless defined $ctime;

    my $pretty_time =
        DateTime->from_epoch( epoch => $ctime )->strftime('%Y-%m-%d.%H:%M:%S');
    print "$marker $pretty_time ", (join '/', @name_elements), "\n";
}


# XXX This thing could use some tests.

=head1 NAME

ceq-read - List all change events in the queue.

=head1 SYNOPSIS

B<ceq-read>

=head1 OPTIONS

    --help      This help page
    --man       Manual page

=head1 SAMPLE OUTPUT

C<A> means attachment, C<P> means page, and C<W> means workspace.

The timestamp is when the change event was noted, in the local time zone.

 A 1973-05-26.18:09:33 workspace/page/attachment_id
 P 1975-05-12.09:06:31 workspace/page
 W 1984-01-01.00:00:01 workspace

=head1 NOTES

Only changes which have not yet been acted upon are listed in the queue.  Once
a job has been forked, its symlink is removed from the queue.

=head1 SEE ALSO

L<ceq-stat(1)>, L<ceqlotron(1)>

=cut
