#!/usr/bin/env perl
# @COPYRIGHT@
use strict;
use warnings;
use DBI;
use Socialtext::AppConfig;
$| = 1;

my $dbh = get_dbh();
my $actual_fk_constraints = get_fk_constraints();
my $expected_fk_constraints = [ map { chomp; $_ } <DATA> ];
check_constraints( $expected_fk_constraints, $actual_fk_constraints );
exit 0;

sub check_constraints {
    my ( $expected, $actual ) = @_;
    my $fail = 0;

    print "Checking for expected foreign key constraints\n";
    for my $key (sort @$expected) {
        unless ( exists $actual->{$key} ) {
            warn "Missing constraint: $key\n";
            $fail = 1;
        }
    }

    if ($fail) {
        die <<MSG

========================================

Some expected foreign key constraints are missing!  Something may have gone
wrong with your upgrade.  Please visit the URL below for more information:

https://www.socialtext.net/stoss/index.cgi?missing_foreign_key_constraints

MSG
    }

    print "Done!  All expected constraints found!\n";
}

sub get_fk_constraints {
    my $sql = 'SELECT conname FROM "pg_constraint" WHERE contype = \'f\'';
    print "Retreiving actual foreign key constraints from the DB\n";
    my %constraints;
    my $rows = $dbh->selectall_arrayref($sql);
    for my $row (@$rows) {
        my ($name) = @$row;
        $constraints{$name} = 1;
    }
    return \%constraints;
}

sub get_dbh {
    my $schema   = Socialtext::AppConfig->db_name();
    my $user     = Socialtext::AppConfig->db_user();
    my $password = Socialtext::AppConfig->db_password();
    my $db_host  = Socialtext::AppConfig->db_host();
    my $host     = $db_host ? ";host=$db_host" : "";
    return DBI->connect(
        "dbi:Pg:dbname=$schema$host",
        $user,
        $password
    );
}

__DATA__
fk_040b7e8582f72e5921dc071311fc4a5f
fk_1541e9b047972328826e1731bc85d4b8
fk_251eb1be4c68e78c9e4b7799c9eed357
fk_2d35adae0767c6ef9bd03ed923bd2380
fk_51604686f50dc445f1d697a101a6a5cb
fk_537b27b50b95eea3e12ec792db0553f5
fk_55d1290a6baacca3b4fec189a739ab5b
fk_777ad60e2bff785f8ff5ece0f3fc95c8
fk_82421c1ae80e2402c554a4bdec97ef4d
fk_82a2b3654e91cdeab69734a8a7e06fa0
fk_84d598c9d334a863af733a2647d59189
fk_c00a18f1daca90d376037f946a0b3894
fk_d9034c52d2999d62d24bd2cfa30ac457
userworkspacerole___role___role_id___role_id___n___1___1___0
watchlist___userid___user_id___system_unique_id___n___1___1___0
workspace___account___account_id___account_id___n___1___1___0
